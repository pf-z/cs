<p>Racket是Lisp语言的一种方言，Lisp最初代表的是“LISt Processor”，意为列表处理器，因此内置的这种list数据结构依然是该语言的突出特性。</p>
<p>Racket中list函数（过程）用于创建列表，该函数接受任意长度的值，然后返回一个包含该值的列表。示例如下</p>
<pre><code class="language-lisp">&gt; (list &quot;red&quot; &quot;green&quot; &quot;blue&quot;)
'(&quot;red&quot; &quot;green&quot; &quot;blue&quot;)
</code></pre>
<p>当然，也可以直接为创建的列表绑定一个标识符</p>
<pre><code class="language-lisp">&gt; (define color (list &quot;red&quot; &quot;green&quot; &quot;blue&quot;))
&gt; color
'(&quot;red&quot; &quot;green&quot; &quot;blue&quot;)
</code></pre>
<p>Racket预定义了一些操作列表的函数，一些示例如下</p>
<pre><code class="language-lisp">&gt; (length color)	; 列表的长度
3
&gt; (list-ref color 0)	; 根据索引提取列表元素	
&quot;red&quot;
&gt; (append color (list &quot;yellow&quot; &quot;white&quot; &quot;black&quot;))	; 追加列表，形成一个新列表
'(&quot;red&quot; &quot;green&quot; &quot;blue&quot; &quot;yellow&quot; &quot;white&quot; &quot;black&quot;)
&gt; color
'(&quot;red&quot; &quot;green&quot; &quot;blue&quot;)
&gt; (define color-ano (append color (list &quot;yellow&quot; &quot;white&quot; &quot;black&quot;)))	; 追加列表，形成一个新列表
&gt; color-ano
'(&quot;red&quot; &quot;green&quot; &quot;blue&quot; &quot;yellow&quot; &quot;white&quot; &quot;black&quot;)
&gt; (reverse color)	; 反转列表
'(&quot;blue&quot; &quot;green&quot; &quot;red&quot;)
&gt; (member &quot;yellow&quot; color)	; 检查某元素是否为列表的元素
#f
&gt; (member &quot;yellow&quot; color-ano)
'(&quot;yellow&quot; &quot;white&quot; &quot;black&quot;)
</code></pre>
<p>上述<code>member</code>函数，如果某元素存在于列表之中，则返回以列表中第一次出现该元素为开头直至结尾的所有元素，否则返回<code>#f</code>。</p>
<h2>预定义的list循环</h2>
<p>考虑到对list进行迭代操作的必要，Racket预定义了<code>map</code>函数，该函数有两类参数，第一类是一个过程，第二类值列表，可以存在多个列表，map函数将会对列表中的每个元素应用输入的过程，然后将过程的返回值重新组合为一个列表，原型如下</p>
<pre><code class="language-lisp">(map proc lst ...+) → list?
  proc : procedure?
  lst : list?
</code></pre>
<p>作为示例，此处对上述定义的color列表各元素拼接一个感叹号，然后得到拼接感叹号之后的新列表，如下</p>
<pre><code class="language-lisp">&gt; (map (lambda (e)
         (string-append e &quot;!&quot;))
       color)
'(&quot;red!&quot; &quot;green!&quot; &quot;blue!&quot;)
</code></pre>
<p>需要特别注意的是，当map函数接受多个列表时，这些列表必须具有同样的长度，而且处理这些列表的函数必须接受每个列表的一个参数。如下的示例程序，对color列表中的元素截取</p>
<pre><code class="language-lisp">&gt; (map (lambda (e i)
         (substring e 0 i))
       color
       (list 2 4 3))
'(&quot;re&quot; &quot;gree&quot; &quot;blu&quot;)
</code></pre>
<p>也可以再增加一个列表，用于控制substring的起始索引，如下</p>
<pre><code class="language-lisp">&gt; (map (lambda (e i j)
         (substring e i j))
       color
       (list 1 2 1)
       (list 2 4 3))
'(&quot;e&quot; &quot;ee&quot; &quot;lu&quot;)
</code></pre>
<p>作为map函数的变种，andmap用于判断列表中的所有元素是否符合给定过程的条件，ormap用于判断列表中是否存在元素满足给定过程的条件，示例如下</p>
<pre><code class="language-lisp">&gt; (andmap string? (list &quot;hello&quot; &quot;world&quot;))
#t
&gt; (andmap string? (list &quot;hello&quot; &quot;world&quot; 2))
#f
&gt; (ormap string? (list &quot;hello&quot; &quot;world&quot; 2))
#t
&gt; (ormap number? (list &quot;hello&quot; &quot;world&quot; 2))
#t
</code></pre>
<p>filter函数用于过滤满足过程条件的列表元素，并返回新的列表</p>
<pre><code class="language-lisp">&gt; (filter number? (list &quot;hello&quot; &quot;world&quot; 2 3 4 5))
'(2 3 4 5)
</code></pre>
<p>一个问题是，如何对一个整型列表数据进行归约操作，比如说求和。在Java之类的非函数式语言中，可以使用循环实现，或者递归，那么对数组进行递归求和的一段js代码如下</p>
<pre><code class="language-javascript">function sum(i, arr) {
    if (i == 0) return arr[0];
    return arr[i] + sum(i - 1, arr);
}

console.log(sum(2, [1, 2, 3]))	// 6
</code></pre>
<p>上述代码也可以这样写</p>
<pre><code class="language-javascript">function sum(i, arr, init) {
    if (i == -1) return init;
    return arr[i] + sum(i - 1, arr, init);
}

console.log(sum(2, [1, 2, 3], 0))	// 6
</code></pre>
<p>至此，很显而易见，init参数提供了求和的初值，为0，此后不断地对每个元素进行迭代操作，最终得出结果。事实上，参数i与arr共同指明了一个元素，即数组中索引为i的元素，所以本质上，sum函数只有两个参数，一个是数组元素，一个是初值。因此，只要具备一个列表中的某个元素与归约操作的初值，就可以进行完整的归约。Racket中直接提供了进行归约操作的函数，其原型为</p>
<pre><code class="language-lisp">(foldl proc init lst ...+) → any/c
  proc : procedure?
  init : any/c
  lst : list?
</code></pre>
<p>proc为具体的规约过程，等同于sum函数本身，init为初值，等同于sum函数的init参数，lst为列表，而列表的元素是由proc显式的给出的，那么利用<code>foldl</code>进行列表归约求列表<code>[1, 2, 3]</code>的和的Racket代码可以写做</p>
<pre><code class="language-lisp">&gt; (foldl (lambda (ele init)
           (+ ele init))
         0
         '(1 2 3))
6
</code></pre>
<p>lambda表达式的参数ele和init分别作为递归操作的两个参数，ele对应的实际参数为列表的每个元素，init对应的实际参数为0。其具体的运作过程为，递归的函数体<code>(+ ele init)</code>每次计算的值都会被重新赋值给init参数，这样init参数实际上就是已处理过的列表元素之和，自然而然地加上当前的列表元素，经过有限次递归之后就可以得到最终的值。类似地，对列表求积的代码如下</p>
<pre><code class="language-lisp">&gt; (foldl (lambda (ele init)
           (* ele init))
         1
         '(1 2 3))
6
</code></pre>
<h2>如何自定义列表迭代函数</h2>
<p>尽管map函数实现了列表迭代的最初定义，但我们显然不满足于此。那么，如何自定义列表迭代呢？</p>
<p>由于列表实际上就是一个链表，所以构建于非空列表(a non-empty list)的两个关键操作是</p>
<ul>
<li>first: 获取非空列表的第一个的元素；</li>
<li>rest: 获取非空列表的除第一个元素之外的其余元素。</li>
</ul>
<p>示例如下</p>
<pre><code class="language-lisp">&gt; (first (list 1 2 3 4 5))
1
&gt; (rest (list 1 2 3 4 5))
'(2 3 4 5)
</code></pre>
<p>这两个关键操作的共性在于其作用的对象必须是一个非空列表，那么如何判断列表是否为空呢？</p>
<p>empty常量用于创建一个空列表，而cons函数用于在已有的列表前面追加元素，cons函数原型如下</p>
<pre><code class="language-lisp">(cons a d) → pair?
  a : any/c
  d : any/c
</code></pre>
<p>只有两个参数，参数类型任意。示例如下</p>
<pre><code class="language-lisp">&gt; empty
'()
&gt; (define ept empty)
&gt; ept
'()
&gt; (cons &quot;head&quot; empty)
'(&quot;head&quot;)
&gt; (cons &quot;dead&quot; (cons &quot;head&quot; empty))
'(&quot;dead&quot; &quot;head&quot;)
</code></pre>
<p>可以使用empty?函数判断列表是否为空，而cons?函数用于判断列表是否为非空。现在我们就可以构造自己的列表迭代函数了。不妨自定义一个计算列表元素个数的函数my-len，如下</p>
<pre><code class="language-lisp">&gt; (define (my-len lst)
    (cond
      [(empty? lst) 0]
      [else (+ 1 (my-len (rest lst)))]))
&gt; (my-len empty)
0
&gt; (my-len (list 1 2 3 4 5 6))
6
</code></pre>
<p>上述代码不过就是递归而已，可以用js代码来解释。由于js中没有类似于rest的函数，因此先实现一个rest函数，然后再实现my_len函数</p>
<pre><code class="language-javascript">function rest(arr) {
    var ret = new Array(arr.length - 1);
    for (var i = 1; i &lt; arr.length; i++) {
        ret[i - 1] = arr[i];
    }
    return ret;
}

function my_len(arr) {
    if (arr.length == 0) return 0;
    return 1 + my_len(rest(arr));
}

my_len([1, 2, 3, 4, 5, 6])	// 6
</code></pre>
<p>这样就很容易理解了。类似地，我们可以实现map函数的功能，如下</p>
<pre><code class="language-lisp">&gt; (define (my-map proc lst)
    (cond
      [(empty? lst) empty]
      [else (cons (proc (first lst))
                  (my-map proc (rest lst)))]))
&gt; (my-map string-upcase (list &quot;Feily&quot; &quot;Zhang&quot;))
'(&quot;FEILY&quot; &quot;ZHANG&quot;)
</code></pre>
<p>再继续实现andmap和ormap以及filter的功能，如下</p>
<pre><code class="language-lisp">&gt; (define (my-andmap proc lst)
    (cond
      [(empty? lst) #t]
      [(proc (first lst)) (my-andmap proc (rest lst))]
      [else #f]))
&gt; (my-andmap string? empty)
#t
&gt; (my-andmap number? empty)
#t
&gt; (my-andmap number? (list 1 2 3))
#t
&gt; (my-andmap number? (list 1 2 3 &quot;str&quot;))
#f
&gt; (my-andmap string? (list 1 2 3 &quot;str&quot;))
#f

&gt; (define (my-ormap proc lst)
    (cond
      [(empty? lst) #f]
      [(proc (first lst)) #t]
      [else (my-ormap proc (rest lst))]))
&gt; (my-ormap string? empty)
#f
&gt; (my-ormap number? empty)
#f
&gt; (my-ormap number? '(1 2 3 &quot;Feily&quot; &quot;Zhang&quot;))
#t
&gt; (my-ormap string? '(1 2 3 &quot;Feily&quot; &quot;Zhang&quot;))
#t

&gt; (define (my-filter proc lst)
    (cond
      [(empty? lst) empty]
      [(proc (first lst)) (cons (first lst) (my-filter proc (rest lst)))]
      [else (my-filter proc (rest lst))]))
&gt; (my-filter number? (list &quot;hello&quot; &quot;world&quot; 2 3 4 5))
'(2 3 4 5)
</code></pre>
<h2>递归与尾递归优化</h2>
<p>递归是Racket进行列表迭代的基本方式，上述递归操作的执行路线为</p>
<pre><code class="language-lisp">(my-len (list 1 2 3))
= (+ 1 (my-len (list 2 3)))
= (+ 1 (+ 1 (my-len (list 3))))
= (+ 1 (+ 1 (+ 1 (my-len (list '())))))	; 递归边界
= (+ 1 (+ 1 (+ 1 0)))
= (+ 1 (+ 1 1))
= (+ 1 2)
= 3
</code></pre>
<p>可以看出，上述递归先是递归前进<code>(my-len (list 1 2 3))</code>，在触碰到递归边界<code>(my-len (list '()))</code>后，然后递归后退<code>(+ 1 (+ 1 (+ 1 0)))</code>，最终得出值。这对于一个长度为n的列表，意味着其空间复杂度达到了_O_(<em>n</em>)。有一种称为尾递归优化的技术来使得对列表的迭代操作为常量空间，其只有递归前进没有递归后退，在触碰到递归边界后就退出，显然，这种方式需要额外的变量来支持，my-len函数可重写如下</p>
<pre><code class="language-lisp">&gt; (define (my-len lst)
    (define (inner-len lst len)
      (cond
        [(empty? lst) len]	; 触碰到边界，直接返回额外参数
        [else (inner-len (rest lst) (+ len 1))]))	; 递归前进的同时更新额外参数
    (inner-len lst 0))
&gt; (my-len (list 1 2 3 4 5 6))
6
</code></pre>
<p>同样的方法，可以将my-map重写如下</p>
<pre><code class="language-lisp">&gt; (define (my-map proc lst)
    (define (inner-map lst ret)
      (cond
        [(empty? lst) (reverse ret)]	; 触碰到边界，直接返回额外参数
        [else (inner-map (rest lst) (cons (proc (first lst)) ret))]))	; 递归前进的同时更新额外参数
    (inner-map lst empty))
&gt; (my-map string-upcase (list &quot;Feily&quot; &quot;Zhang&quot;))
'(&quot;FEILY&quot; &quot;ZHANG&quot;)
</code></pre>
<h2>递归之于迭代</h2>
<p>从某种程度上说，迭代只是递归的一种特例。多数语言中，尝试将尽可能多的计算适应于迭代是非常重要的，换言之，迭代是多数语言在语言层面提供的重要特性（语法），比如for、while、do-while等循环，因为递归的性能相当低下。Racket中对于递归的这一缺陷使用尾递归优化来弥补，这样可以将递归的空间复杂度由_O_(<em>n</em>)将到常数级。</p>
<p>但是，在Racket当中，递归并不会导致糟糕的性能，也不存在堆栈溢出。如果运算涉及太多的上下文，则可能耗尽内存，但是相对于其它语言来说，Racket可以承受更深数量级的递归。基于这些考虑，在加上尾递归优化技术，Racket并没有回避递归这一程序设计思维，而是选择拥抱它。</p>
